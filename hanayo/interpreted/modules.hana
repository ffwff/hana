__modules = Record()
function import(x) begin
    if not x.endswith?(".hana") x = x + ".hana"
    if x.startswith?("./") begin
        // resolve relative path
        dirs = __file__.split("/")
        dirs.pop()
        x = realpath("/" + dirs.join("/") + "/" + x.delete(0, 2))
    end
    else if x.startswith?("/") begin
        x = realpath(x)
    end
    else begin
        // resolve builtin
        env = Env::get?("HANA_PATH")
        if env begin
            name = x
            path = env+"/"+name
            x = realpath(path)
            if x == nil panic!("can't find module " + name + "!")
        end
        else begin
            panic!("can't find module dir, environment variable HANA_PATH isn't set!")
            return false
        end
    end
    if x == nil return false

    // don't import if it's already imported
    if __modules[x] return false

    // save __file__ and import!
    old_file = ^__file__
    ^__file__ = x
    f = File(x, "r")
    if eval(f.readall()) begin // success
        __modules[x] = true
        ^__file__ = old_file
        return true
    end
    else begin // failed
        ^__file__ = old_file
        return false
    end
end
